buildscript {
	repositories {
		maven { url 'http://repo.spring.io/plugins-release' }
	}
	dependencies {
		classpath 'io.spring.gradle:spring-io-plugin:0.0.5.RELEASE'
	}
}

plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'jacoco'
	id 'org.sonarqube' version '1.2'
	id 'checkstyle'
}
description = 'Spring Integration AWS Support'

apply from: "${rootProject.projectDir}/publish-maven.gradle"

group = 'org.springframework.integration'

repositories {
	if (version.endsWith('BUILD-SNAPSHOT') || project.hasProperty('platformVersion')) {
		maven { url 'https://repo.spring.io/libs-snapshot' }
	}
	maven { url 'http://repo.spring.io/libs-milestone' }
}

compileJava {
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
}

compileTestJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

if (project.hasProperty('platformVersion')) {
	apply plugin: 'spring-io'

	dependencyManagement {
		springIoTestRuntime {
			imports {
				mavenBom "io.spring.platform:platform-bom:${platformVersion}"
			}
		}
	}
}

ext {
	assertjVersion = '3.5.2'
	servletApiVersion = '3.1.0'
	slf4jVersion = '1.7.21'
	springCloudAwsVersion = '1.1.1.RELEASE'
	springIntegrationVersion = '4.3.2.RELEASE'

	idPrefix = 'aws'

	linkHomepage = 'https://github.com/spring-projects/spring-integration-aws'
	linkCi = 'https://build.spring.io/browse/INTEXT'
	linkIssue = 'https://jira.spring.io/browse/INTEXT'
	linkScmUrl = 'https://github.com/spring-projects/spring-integration-aws'
	linkScmConnection = 'https://github.com/spring-projects/spring-integration-aws.git'
	linkScmDevConnection = 'git@github.com:spring-projects/spring-integration-aws.git'
}

ext.javadocLinks = [
		"http://docs.aws.amazon.com/AWSJavaSDK/latest/javadoc/"
] as String[]

jacoco {
	toolVersion = "0.7.7.201606060606"
}

checkstyle {
	configFile = file("${rootDir}/src/checkstyle/checkstyle.xml")
	toolVersion = "6.16.1"
}

dependencies {

	compile "org.springframework.integration:spring-integration-core:$springIntegrationVersion"
	compile "org.springframework.cloud:spring-cloud-aws-core:$springCloudAwsVersion"
	compile("org.springframework.cloud:spring-cloud-aws-messaging:$springCloudAwsVersion", optional)
	compile("org.springframework.integration:spring-integration-file:$springIntegrationVersion", optional)
	compile("org.springframework.integration:spring-integration-http:$springIntegrationVersion", optional)
	compile("javax.servlet:javax.servlet-api:$servletApiVersion", provided)

	testCompile "org.springframework.integration:spring-integration-test:$springIntegrationVersion"
	testCompile "org.assertj:assertj-core:$assertjVersion"

	testRuntime "org.slf4j:slf4j-log4j12:$slf4jVersion"
}

eclipse.project.natures += 'org.springframework.ide.eclipse.core.springnature'

javadoc {
	group = 'Documentation'
	description = 'Generates the Javadoc API documentation.'
	title = "${rootProject.description} ${version} API"

	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.header = project.description
	options.overview = 'src/api/overview.html'
	options.stylesheetFile = file("src/api/stylesheet.css")
	options.splitIndex = true
	options.links(project.ext.javadocLinks)

	source = sourceSets.main.allJava
	classpath = project.sourceSets.main.compileClasspath
	destinationDir = new File(buildDir, "api")

	// suppress warnings due to cross-module @see and @link references;
	// note that global 'api' task does display all warnings.
	logging.captureStandardError LogLevel.INFO
	logging.captureStandardOutput LogLevel.INFO // suppress "## warnings" message
}

sourceSets {
	test {
		resources {
			srcDirs = ['src/test/resources', 'src/test/java']
		}
	}
}

// enable all compiler warnings; individual projects may customize further
ext.xLintArg = '-Xlint:all,-options'
[compileJava, compileTestJava]*.options*.compilerArgs = [xLintArg]

test {
	maxHeapSize = "1024m"
	jacoco {
		append = false
		destinationFile = file("$buildDir/jacoco.exec")
	}
}

tasks.withType(Test).all {
	// suppress all console output during testing unless running `gradle -i`
	logging.captureStandardOutput(LogLevel.INFO)
}

jacocoTestReport {
	reports {
		xml.enabled false
		csv.enabled false
		html.destination "${buildDir}/reports/jacoco/html"
	}
}

build.dependsOn jacocoTestReport

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

sonarqube {
	properties {
		property "sonar.jacoco.reportPath", "${buildDir.name}/jacoco.exec"
		property "sonar.links.homepage", linkHomepage
		property "sonar.links.ci", linkCi
		property "sonar.links.issue", linkIssue
		property "sonar.links.scm", linkScmUrl
		property "sonar.links.scm_dev", linkScmDevConnection
		property "sonar.java.coveragePlugin", "jacoco"
	}
}

task schemaZip(type: Zip) {
	group = 'Distribution'
	classifier = 'schema'
	description = "Builds -${classifier} archive containing all " +
			"XSDs for deployment at static.springframework.org/schema."

	def Properties schemas = new Properties();
	def shortName = idPrefix.replaceFirst("${idPrefix}-", '')

	project.sourceSets.main.resources.find {
		it.path.endsWith("META-INF${File.separator}spring.schemas")
	}?.withInputStream { schemas.load(it) }

	for (def key : schemas.keySet()) {
		File xsdFile = project.sourceSets.main.resources.find {
			it.path.replaceAll('\\\\', '/').endsWith(schemas.get(key))
		}
		assert xsdFile != null
		into("integration/${shortName}") {
			from xsdFile.path
		}
	}

}

task docsZip(type: Zip) {
	group = 'Distribution'
	classifier = 'docs'
	description = "Builds -${classifier} archive containing the JavaDoc api " +
			"for deployment at static.springframework.org/spring-integration/docs."

	from('.') {
		include 'README.md'
	}

	from(javadoc) {
		into 'api'
	}

}

task distZip(type: Zip, dependsOn: [docsZip, schemaZip]) {
	group = 'Distribution'
	classifier = 'dist'
	description = "Builds -${classifier} archive, containing all jars and docs, " +
			"suitable for community download page."

	ext.baseDir = "${project.name}-${project.version}";

	from('src/dist') {
		include 'license.txt'
		include 'notice.txt'
		into "${baseDir}"
	}

	from('.') {
		include 'README.md'
		into "${baseDir}"
	}

	from(zipTree(schemaZip.archivePath)) {
		into "${baseDir}/schema"
	}

	into("${baseDir}/libs") {
		from project.jar
		from project.sourcesJar
		from project.javadocJar
	}
}

// Create an optional "with dependencies" distribution.
// Not published by default; only for use when building from source.
task depsZip(type: Zip, dependsOn: distZip) { zipTask ->
	group = 'Distribution'
	classifier = 'dist-with-deps'
	description = "Builds -${classifier} archive, containing everything " +
			"in the -${distZip.classifier} archive plus all dependencies."

	from zipTree(distZip.archivePath)

	gradle.taskGraph.whenReady { taskGraph ->
		if (taskGraph.hasTask(":${zipTask.name}")) {
			def projectName = rootProject.name
			def artifacts = new HashSet()

			rootProject.configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
				def dependency = artifact.moduleVersion.id
				if (!projectName.equals(dependency.name)) {
					artifacts << artifact.file
				}
			}

			zipTask.from(artifacts) {
				into "${distZip.baseDir}/deps"
			}
		}
	}
}

artifacts {
	archives distZip
	archives docsZip
	archives schemaZip
}

task dist(dependsOn: assemble) {
	group = 'Distribution'
	description = 'Builds -dist, -docs and -schema distribution archives.'
}
